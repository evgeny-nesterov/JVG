buildscript {
    repositories {
        mavenLocal()
        maven { url "http://download.osgeo.org/webdav/geotools/" }
        // before central - it's workaround for jai_core dependency
        mavenCentral()
    }
}

allprojects {
    apply plugin: 'idea'

    idea {
        module {
            inheritOutputDirs = false
            outputDir = file('bin')
            testOutputDir = file('bin')
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    sourceCompatibility = 20
    targetCompatibility = 20
    compileJava.options.encoding = "windows-1251"

    repositories {
        mavenLocal()
        mavenCentral()
    }

    eclipse {
        jdt {
            sourceCompatibility = 20
            targetCompatibility = 20
        }
        classpath { containers 'org.springsource.ide.eclipse.gradle.classpathcontainer' }
    }

    version = '1.0'

    jar {
        exclude {
            it.file.getAbsolutePath().startsWith(sourceSets.main.output.resourcesDir.getAbsolutePath())
        }
        manifest.attributes provider: 'E.Nesterov'
    }
}

project(':jvg') {
    dependencies {
        implementation project(':hiscript')
        implementation project(':toi')
        implementation project(':satisui')

        implementation("org.slf4j:slf4j-api:1.7.7")
        implementation("org.jdom:jdom:2.0.2")
        implementation group: 'org.w3c.css', name: 'sac', version: '1.3'
        implementation group: 'net.sourceforge.cssparser', name: 'cssparser', version: '0.9.23'
        implementation files('../libs/jai_codec-1.1.3.jar')
        implementation files('../libs/jai_core-1.1.3.jar')
    }
}

project(':hiscript') {
    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    }

    test {
        useJUnitPlatform()
    }
}

project(':toi') {
    dependencies {
        implementation project(':satisui')
        implementation("org.jdom:jdom:2.0.2")
        implementation files('../libs/jai_codec-1.1.3.jar')
        implementation files('../libs/jai_core-1.1.3.jar')
    }
}

project(':satisui') {
}
